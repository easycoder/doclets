!   docletServer.ecs

    script DocletServer

    use mqtt
    use plugin Doclets from ec_doclets

    topic ServerTopic
    topic SenderTopic
    dictionary Sender
    dictionary ReceivedMessage
    dictionary WaitForConfirmation
    list ResultList
    variable MQTT_Token
    variable MyID
    variable SenderName
    variable Topic
    variable Topics
    variable Action
    variable SenderQoS
    variable MessageText
    variable ConfirmationRequested
    
!    debug step

    reset WaitForConfirmation
    clear ConfirmationRequested

    ! Get the MQTT token
    if file `~/.mqqt_token` does not exist
    begin
        print `No MQTT token could be found.`
        exit
    end
    load MQTT_Token from `~/.mqqt_token`
    print `Token is ` cat MQTT_Token

    ! Set up MQTT
    put `a8:41:f4:d3:19:dd/request` into MyID
    init ServerTopic
        name MyID
        qos 1

    mqtt
        token MQTT_Token
        id uuid
        broker `mqtt.flespi.io`
        port 8883
        subscribe ServerTopic

    doclets init
    
    on mqtt message
    begin
        put the mqtt message into ReceivedMessage
!        log `Incoming: ` cat ReceivedMessage
        put entry `topic` of ReceivedMessage into Topic
        put entry `sender` of ReceivedMessage into Sender
        put entry `action` of ReceivedMessage into Action
        put entry `name` of Sender into SenderName
        put entry `qos` of Sender into SenderQoS

        if Action is `topics` go to GetTopics
        else if Action is `query` go to DoQuery
        else if Action is `view` go to GetDoclet
        else if Action is `confirm` go to DoConfirm
    end
    stop

! Get a list of the available doclet topics
GetTopics:
    put the doclet topics into Topics
    put Topics into MessageText
    log `Available topics: ` cat MessageText
!    set ConfirmationRequested
    gosub to SendReply
    stop

! Process a query
DoQuery:
    log `Query in ` cat Topic cat ` for ` cat ReceivedMessage
    get doclet ResultList from ReceivedMessage
    if the count of ResultList is 0 log `No results`
    else log `Sending ` cat left 40 of ResultList
    put ResultList into MessageText
    gosub to SendReply
    stop

! Get the content of a doclet
GetDoclet:
    get doclet MessageText from ReceivedMessage
    gosub to SendReply
    stop

! Process a confirmation
DoConfirm:
    if WaitForConfirmation has entry Sender
    begin
        log `Confirmation from ` cat Sender
        clear entry Sender of WaitForConfirmation
    end
    else log `No confirmation requested`
    stop

! Send a reply message
SendReply:
    init SenderTopic
        name SenderName
        qos SenderQoS
    if ConfirmationRequested
    begin
        set entry Sender of WaitForConfirmation
        set Action to `confirm`
    end
    else set Action to `reply`
    send to SenderTopic
        action Action
        message MessageText
    clear ConfirmationRequested
    return
