!   docletServer.ecs

    script DocletServer

    use mqtt
    use plugin Doclets from ec_doclets

    topic ServerTopic
    topic SenderTopic
    dictionary Sender
    dictionary ReceivedMessage
    dictionary WaitForConfirmation
    list ResultList
    queue MessageQueue
    variable MQTT_Token
    variable MyID
    variable SenderName
    variable Topic
    variable Topics
    variable Action
    variable SenderQoS
    variable MessageText
    variable ConfirmationRequested
    
!    debug step

    reset WaitForConfirmation
    clear ConfirmationRequested

    ! Get the MQTT token
    if file `~/.mqqt_token` does not exist
    begin
        print `No MQTT token could be found.`
        exit
    end
    load MQTT_Token from `~/.mqqt_token`
    print `Token is ` cat MQTT_Token

    ! Set up MQTT
    put `a8:41:f4:d3:19:dd/request` into MyID
    init ServerTopic
        name MyID
        qos 1

    mqtt
        token MQTT_Token
        id uuid
        broker `mqtt.flespi.io`
        port 8883
        subscribe ServerTopic

    doclets init
    
    on mqtt message append the mqtt message to MessageQueue

    while true
    begin
        wait 50 ticks
        if MessageQueue is not empty
        begin
            pop ReceivedMessage from MessageQueue
!            log `Incoming: ` cat ReceivedMessage
            put entry `sender` of ReceivedMessage into Sender
            put entry `action` of ReceivedMessage into Action
            put entry `message` of ReceivedMessage into MessageText
            put entry `name` of Sender into SenderName
            put entry `qos` of Sender into SenderQoS

            if Action is `topics` gosub to GetTopics
            else if Action is `query` gosub to DoQuery
            else if Action is `view` gosub to GetDoclet
            else if Action is `confirm` gosub to DoConfirm
        end
    end
    stop

! Get a list of the available doclet topics
GetTopics:
    put the doclet topics into Topics
    put Topics into MessageText
!    log `Available topics: ` cat MessageText
    go to SendReply

! Process a query
DoQuery:
!    log `Query: ` cat Message
    get doclet ResultList from ReceivedMessage
    if the count of ResultList is 0 log `No results`
    else log `Sending ` cat left 40 of ResultList
    put ResultList into MessageText
    go to SendReply

! Get the content of a doclet
GetDoclet:
    get doclet MessageText from ReceivedMessage
    go to SendReply

! Process a confirmation
DoConfirm:
    if WaitForConfirmation has entry Sender
    begin
        log `Confirmation from ` cat Sender
        clear entry Sender of WaitForConfirmation
    end
    else log `No confirmation requested`
    return

! Send a reply message
SendReply:
    init SenderTopic
        name SenderName
        qos SenderQoS
    if ConfirmationRequested
    begin
        set entry Sender of WaitForConfirmation
        set Action to `confirm`
    end
    else set Action to `reply`
    send to SenderTopic
        action Action
        message MessageText
    clear ConfirmationRequested
    return
